module = { SOI ~ (module_doc)* ~ (import | alias)* ~ EOI }

alias = { line_doc* ~ name ~ "=" ~ descriptor }

name = @{ (ASCII_ALPHA)+ }

// Descriptors

descriptor = @{ "|"? ~ WHITESPACE* ~ descriptor_inner ~ (WHITESPACE* ~ "|" ~ WHITESPACE* ~ descriptor_inner)* }

descriptor_inner = !{ literal | inline_import | primitive | object | array | tuple | alias | name }

primitive = { "string" | "int" | "float" | "boolean" | "null" }

// Literals

literal = { literal_string | literal_float | literal_integer | literal_boolean }

literal_string = ${ quote ~ string_inner ~ quote }

quote = _{ "\"" }

string_inner = @{ (!("\"" | "\\") ~ ANY)* ~ (string_escape ~ string_inner)? }

string_escape  = @{ "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | string_unicode) }

string_unicode = @{ "u" ~ ASCII_HEX_DIGIT{4} }

literal_integer = @{ ("+" | "-")? ~ integer }

literal_float = @{ ("+" | "-")? ~ integer ~ ("." ~ digits ~ exponent? | exponent) }

integer =  @{ "0" | (ASCII_NONZERO_DIGIT ~ digits?) }

digits = @{ (ASCII_DIGIT | ("_" ~ ASCII_DIGIT))+ }

exponent = @{ ("E" | "e") ~ ("+" | "-")? ~ ASCII_DIGIT+ }

literal_boolean = ${ "true" | "false" }

// Objects

object = { "{" ~ properties? ~ "}" }

properties = _{ any_property ~ (","? ~ any_property)* ~ ","? }

any_property = _{ extension_property | optional_property | required_property }

extension_property = { "..." ~ name }

required_property = { line_doc* ~ name ~ ":" ~ descriptor }

optional_property = { line_doc* ~ name ~ "?:" ~ descriptor }

// Arrays

array = { "[" ~ descriptor ~ "]" }

// Tuples

tuple = { "(" ~ descriptor ~ ("," ~ descriptor)* ~ ")" }

// Modules

import = { "use" ~ import_reference }

import_reference = ${ path ~ (import_glob | import_names | name) }

import_names = !{ "{" ~ (import_name ~ ("," ~ import_name)*)? ~ "}" }

import_name = { name ~ ("as" ~ name)? }

import_glob = ${ "*" }

inline_import = @{ path ~ name }

path = @{ path_segment+ }

path_segment = ${ "../" | "./" | ((!"/" ~ !NEWLINE ~ ANY)+ ~ "/") }

// Comments

line_comment = _{ ("//" ~ !("/" | "!") ~ (!NEWLINE ~ ANY)*) }

block_comment = _{ "/*" ~ (block_comment | !"*/" ~ ANY)* ~ "*/" }

module_doc = ${ "//!" ~ SPACE? ~ doc }

line_doc = ${ "///" ~ SPACE? ~ doc }

doc = @{ (!NEWLINE ~ ANY)* }

// Base

SPACE = _{ " " | "\t" }

WHITESPACE = _{ SPACE | NEWLINE }

COMMENT = _{ line_comment | block_comment }
