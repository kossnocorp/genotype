module = { SOI ~ (module_doc)* ~ (alias)* ~ EOI }

alias = { line_doc* ~ name ~ "=" ~ (any_descriptor) }

name = @{ (ASCII_ALPHA)+ }

// Descriptor

any_descriptor = _{ nullable_descriptor | descriptor  }

descriptor = !{ primitive | object | alias | name }

nullable_descriptor = ${ descriptor ~ "?" }

primitive = { "string" | "int" | "float" | "boolean" }

// Object

object = { "{" ~ properties? ~ "}" }

properties = _{ any_property ~ (","? ~ any_property)* ~ ","? }

any_property = _{ optional_property | required_property }

required_property = { line_doc* ~ name ~ ":" ~ any_descriptor }

optional_property = { line_doc* ~ name ~ "?:" ~ any_descriptor }

// Comments

line_comment = _{ ("//" ~ !("/" | "!") ~ (!NEWLINE ~ ANY)*) }

block_comment = _{ "/*" ~ (block_comment | !"*/" ~ ANY)* ~ "*/" }

module_doc = ${ "//!" ~ SPACE? ~ doc }

line_doc = ${ "///" ~ SPACE? ~ doc }

doc= @{ (!NEWLINE ~ ANY)* }

// Base

SPACE = _{ " " | "\t" }

WHITESPACE = _{ SPACE | NEWLINE }

COMMENT = _{ line_comment | block_comment }