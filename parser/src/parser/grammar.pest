file = { SOI ~ (file_comment)* ~ (alias)* ~ EOI }

alias = { line_doc* ~ const ~ "=" ~ (any_type | const) }

const = @{ (ASCII_ALPHA)+ }

// Types

any_type = _{ optional_type | type  }

type = !{ primitive | struct }

optional_type = ${ type ~ "?" }

primitive = { "string" | "number" | "float" | "boolean" }

// Structs

struct = { "{" ~ properties? ~ "}" }

properties = { any_property ~ (","? ~ any_property)* ~ ","? }

any_property = _{ line_doc* ~ (optional_property | property) }

property = { const ~ ":" ~ any_type }

optional_property = { const ~ "?:" ~ any_type }

// Comments

line_comment = _{ ("//" ~ !("/" | "!") ~ (!NEWLINE ~ ANY)*) }

block_comment = _{ "/*" ~ (block_comment | !"*/" ~ ANY)* ~ "*/" }

file_comment = ${ "//!" ~ SPACE? ~ comment_content }

line_doc = ${ "///" ~ SPACE? ~ comment_content }

comment_content = @{ (!NEWLINE ~ ANY)* }

// Base

SPACE = _{ " " | "\t" }

WHITESPACE = _{ SPACE | NEWLINE }

COMMENT = _{ line_comment | block_comment }